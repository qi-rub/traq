// The Triangle Finding algorithm from Triangle-finding problem: 
// first para https://arxiv.org/pdf/quant-ph/0007016
// + also, case-study in Quipper

declare Adj(Fin<N>, Fin<N>) -> Bool end

def CompletesTriangle(i: Fin<N>, j: Fin<N>, k: Fin<N>) -> Bool do
  eq_ij <- i == j;
  neq_ij <- not eq_ij;

  eq_jk <- j == k;
  neq_jk <- not eq_jk;

  eq_ki <- k == i;
  neq_ki <- not eq_ki;
  
  distinct2 <- neq_ij && neq_jk;
  distinct3 <- distinct2 && neq_ki;

  edge_jk <- Adj(j, k);
  edge_ki <- Adj(k, i);

  temp <- distinct3 && edge_jk;
  result <- temp && edge_ki;

  return result
end

def HasTriangleCycle(ij: Tup<Fin<N>, Fin<N>>) -> Bool do
  i <- ij.0;
  j <- ij.1;
  edge_ij <- Adj(i, j);
  found_k <- @any<Fin<N>>[CompletesTriangle(i, j, _)];

  result <- edge_ij && found_k;
  
  return result
end

def HasTriangle() -> Bool do
  found_triangle <- @any<Tup<Fin<N>, Fin<N>>>[HasTriangleCycle(_)];
  return found_triangle
end